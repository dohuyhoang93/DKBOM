@echo off
chcp 65001 >nul
setlocal ENABLEEXTENSIONS ENABLEDELAYEDEXPANSION

call :box "WATCHDOG TRACKING UiPATH PROCESS"

:: === C·∫§U H√åNH ===
set "LOCK_FILE=%USERPROFILE%\Documents\LogUiPath\STATUS.lock"
set "INPUTJSON=%USERPROFILE%\Documents\LogUiPath\input.json"
set "UIPATH_EXE=%USERPROFILE%\AppData\Local\Programs\UiPath\Studio\UiRobot.exe"
set "PROJECT_PATH_SEARCH=%USERPROFILE%\Documents\*.nupkg"
set "PROJECT_PATH="
set "MAX_RETRY=3"
set /a WAIT_SECONDS=240
set /a WAIT_SECONDS_EXIT_FILE=45
set /a RETRY_COUNT=0
set "PREV_FILETIME="
call :SEARCH_NUPKG

echo [*] Watchdog initial complete

call :section "Watchdog Running ..."

echo [~] ƒêang kh·ªüi ch·∫°y ti·∫øn tr√¨nh UiPath l·∫ßn ƒë·∫ßu...
start "run uipath" /b "%UIPATH_EXE%" execute --file "%PROJECT_PATH%"
echo [~] Ho√†n th√†nh kh·ªüi ƒë·ªông ti·∫øn tr√¨nh UiPath
call :done

:: === MAIN V√íNG L·∫∂P WATCHDOG ===
:RETRY_LOOP
echo [~] Watchdog is running
echo [.] ƒê·ª£i %WAIT_SECONDS_EXIT_FILE% gi√¢y ƒë·ªÉ UiPath kh·ªüi t·∫°o...

timeout /t %WAIT_SECONDS_EXIT_FILE% /nobreak >nul

echo [.] ƒêang ki·ªÉm tra file: %LOCK_FILE%
if not exist "%LOCK_FILE%" (
    echo [*] File lock kh√¥ng t·ªìn t·∫°i. X·ª≠ l√Ω kh·ªüi ch·∫°y l·∫°i UiPath...
    goto :RESTART_UIPATH
)

call :GET_FILE_MODTIME
set "PREV_FILETIME=!FILETIME!"
echo [+] Th·ªùi gian s·ª≠a tr∆∞·ªõc: !PREV_FILETIME!

echo [.] ƒê·ª£i %WAIT_SECONDS% gi√¢y...
timeout /t %WAIT_SECONDS% /nobreak >nul

if not exist "%LOCK_FILE%" (
    echo [*] File lock ƒë√£ b·ªã x√≥a. Kh·ªüi ƒë·ªông l·∫°i UiPath...
    goto :RESTART_UIPATH
)

:: ƒê·ªçc d√≤ng ƒë·∫ßu ti√™n c·ªßa file .lock v√†o bi·∫øn STATUS
set /p STATUS=<"%LOCK_FILE%"

:: So s√°nh v√† exit n·∫øu n·ªôi dung l√† FINISH
if /i "%STATUS%"=="FINISH" (
    echo [*] Ti·∫øn tr√¨nh UiPath ƒë√£ ho√†n t·∫•t.
    call :done
    call :section "Watchdog Exiting"
    pause
    exit
)

call :GET_FILE_MODTIME
echo [+] Th·ªùi gian s·ª≠a sau: !FILETIME!

if "!FILETIME!"=="!PREV_FILETIME!" (
    echo [*] File kh√¥ng ƒë∆∞·ª£c c·∫≠p nh·∫≠t. X·ª≠ l√Ω kh·ªüi ch·∫°y l·∫°i UiPath...
    goto :RESTART_UIPATH
)

echo [+] File ƒë∆∞·ª£c c·∫≠p nh·∫≠t. Ti·∫øn tr√¨nh ƒëang ho·∫°t ƒë·ªông t·ªët.
set /a RETRY_COUNT=0
goto :RETRY_LOOP


:: === H√ÄM T√åM FILE NUPKG ===
:SEARCH_NUPKG
REM Ki·ªÉm tra xem bi·∫øn ƒë√£ ƒë∆∞·ª£c g√°n ƒë√∫ng ch∆∞a
ECHO [~] Search nupkg file in: %PROJECT_PATH_SEARCH%

REM L·∫∑p qua c√°c t·ªáp kh·ªõp v·ªõi m·∫´u
FOR %%F IN ("%PROJECT_PATH_SEARCH%") DO (
    REM ECHO Found nupkg file: "%%F"
	SET "PROJECT_PATH=%%F"
	echo [.] Found nupkg file: !PROJECT_PATH!
	REM GOTO :EOF kh·ªõp v·ªõi file cu·ªëi c√πng matching
)

REM N·∫øu kh√¥ng c√≥ t·ªáp n√†o kh·ªõp, v√≤ng l·∫∑p FOR s·∫Ω kh√¥ng th·ª±c thi g√¨ c·∫£.
IF NOT EXIST "%PROJECT_PATH%" (
    ECHO [.] No .nupkg files starting with DKBOM found in %PROJECT_PATH_SEARCH%
	call :section "Watchdog Stopped"
	pause
	exit
)

echo [*] Set nupkg project file is: %PROJECT_PATH%
GOTO :EOF

:: === H√ÄM X·ª¨ L√ù KH·ªûI ƒê·ªòNG L·∫†I ===
:RESTART_UIPATH
if %RETRY_COUNT% GEQ %MAX_RETRY% (
    echo [*] ƒê√£ th·ª≠ %MAX_RETRY% l·∫ßn nh∆∞ng file lock kh√¥ng c·∫≠p nh·∫≠t. X√≥a input.json v√† tho√°t watchdog.
	echo [.] ƒê·ª£i %WAIT_SECONDS_EXIT_FILE% gi√¢y...
	timeout /t %WAIT_SECONDS_EXIT_FILE% /nobreak >nul
	del %INPUTJSON%
	call :section "Watchdog Stopped"
	pause
    exit
)
set /a RETRY_COUNT+=1
echo [*] ƒêang th·ª≠ kh·ªüi ƒë·ªông l·∫°i l·∫ßn th·ª© %RETRY_COUNT%...

echo [~] ƒê√≥ng to√†n b·ªô ti·∫øn tr√¨nh UiPath v√† Ksystem...
taskkill /f /im UiPath.Studio.exe >nul 2>&1
taskkill /f /im UiPath.Executor.exe >nul 2>&1
taskkill /f /im UiPath.Agent.exe >nul 2>&1
taskkill /f /im Angkor.Ylw.Main.MainWin45.exe >nul 2>&1

echo [~] ƒêang kh·ªüi ch·∫°y l·∫°i ti·∫øn tr√¨nh UiPath...
start "run uipath" /b "%UIPATH_EXE%" execute --file "%PROJECT_PATH%"
echo [*] Ho√†n th√†nh kh·ªüi ƒë·ªông l·∫°i ti·∫øn tr√¨nh UiPath
call :done
goto :RETRY_LOOP

:: === H√ÄM L·∫§Y TH·ªúI GIAN ===
:GET_FILE_MODTIME
set "FILETIME="
for %%F in ("%LOCK_FILE%") do (
    set "FILETIME=%%~tF"
)
echo [~] ƒê√£ ki·ªÉm tra th·ªùi ƒëi·ªÉm modify STATUS.lock
goto :EOF

:: === H√ÄM HI·ªÇN TH·ªä ===
:section
echo.
echo =======================================================
echo üî∑ %~1
echo =======================================================
echo.
goto :eof

:done
echo.
echo ‚úî DONE!
echo -------------------------------------------------------
echo.
goto :eof

:box
setlocal enabledelayedexpansion
set "msg=%~1"

:: T√≠nh ƒë·ªô d√†i chu·ªói g·ªëc
set "str=%msg%"
set /a len=0
:strlen_loop
if defined str (
    set "str=!str:~1!"
    set /a len+=1
    goto strlen_loop
)

:: C·ªông th√™m 2 k√Ω t·ª± padding (1 m·ªói b√™n)
set /a plen=len + 2

:: T·∫°o d√≤ng tr√™n
set "top=‚îå"
for /l %%i in (1,1,%plen%) do set "top=!top!‚îÄ"
set "top=!top!‚îê"

:: T·∫°o d√≤ng gi·ªØa, c√≥ padding tr√°i v√† ph·∫£i
set "mid=‚îÇ !msg! ‚îÇ"

:: T·∫°o d√≤ng d∆∞·ªõi
set "bot=‚îî"
for /l %%i in (1,1,%plen%) do set "bot=!bot!‚îÄ"
set "bot=!bot!‚îò"

:: In ra
echo !top!
echo !mid!
echo !bot!
endlocal
exit /b